model com.arsdigita.cms.contenttypes;

import com.arsdigita.kernel.ACSObject;
import com.arsdigita.cms.*;

object type SciOrganizationWithPublications extends SciOrganization {
    reference key ( ct_sciorga_organizations_with_publications.organization_id );
}

association {
    SciOrganization[0..n] organizations = join ct_publications.publication_id
                                            to ct_organization_publication_map.publication_id,
                                          join ct_organization_publication_map.organization_id
                                            to ct_sciorga_organizations_with_publications.organization_id;

    Publication[0..n] publications = join ct_sciorga_organizations_with_publications.organization_id
                                       to ct_organization_publication_map.organization_id,
                                     join ct_organization_publication_map.publication_id
                                       to ct_publications.publication_id;     

    Integer[0..1] publicationOrder = ct_organization_publication_map.publication_order INTEGER;                                       
}

query getIdsOfPublicationsOfSciOrganization {
    BigDecimal publicationId;
    String objectType;

    do {
        select ct_organization_publication_map.publication_id, acs_objects.object_type
        from ct_organization_publication_map join acs_objects on ct_organization_publication_map.publication_id = acs_objects.object_id
        where ct_organization_publication_map.organization_id = :organization
    } map {
        publicationId = ct_organization_publication_map.publication_id;
        objectType = acs_objects.object_type;
    }
}

query getIdsOfWorkingPapersOfSciOrganization {
    BigDecimal workingPaperId;
    String objectType;

    do {
        select ct_organization_publication_map.publication_id, acs_objects.object_type
        from ct_organization_publication_map join acs_objects on ct_organization_publication_map.publication_id = acs_objects.object_id
        where ct_organization_publication_map.organization_id = :organization and acs_objects.object_type = 'com.arsdigita.cms.contenttypes.WorkingPaper'
    } map {
        workingPaperId = ct_organization_publication_map.publication_id;
        objectType = acs_objects.object_type;
    }
}

query getAllYearsOfPublication {
    Integer yearOfPublication;

    do {
        select distinct ct_publications.year 
        from ct_publications
    } map {
        yearOfPublication = ct_publications.year;
    }
}

query getAllYearsOfPublicationForAuthor {
    Integer yearOfPublication;

    do {
        select distinct ct_publications.year 
        from ct_publications join  ct_publications_authorship on ct_publications.publication_id = ct_publications_authorship.publication_id 
        where person_id = :author
    } map {
        yearOfPublication = ct_publications.year;
    }
}

query getAllPublicationTypes {

    String objectType;
    
    do {
        select distinct acs_objects.object_type 
        from ct_publications join acs_objects on ct_publications.publication_id = acs_objects.object_id        
    } map {
        objectType = acs_objects.object_type;
    }
}

query getAllPublicationTypesForAuthor {

    String objectType;

    do {
        select distinct acs_objects.object_type 
        from ct_publications join acs_objects on ct_publications.publication_id = acs_objects.object_id join ct_publications_authorship on ct_publications.publication_id = ct_publications_authorship.publication_id 
        where person_id = :author
    } map {
        objectType = acs_objects.object_type;
    }

}

query getIdsOfPublicationsOfSciMember {
    BigDecimal publicationId;
    
    do {
        select ct_publications_authorship.publication_id 
        from ct_publications_authorship 
        where ct_publications_authorship.person_id = :author
    } map {
        publicationId = ct_publications_authorship.publication_id;
    }
}

query getIdsOfProjectsOfSciMember {
    BigDecimal projectId;

    do {
        select ct_sciorga_projects.project_id
        from ct_sciorga_projects join cms_organizationalunits_person_map on ct_sciorga_projects.project_id = cms_organizationalunits_person_map.organizationalunit_id
        where cms_organizationalunits_person_map.person_id = :member
    } map {
        projectId = ct_sciorga_projects.project_id;
    }
}
