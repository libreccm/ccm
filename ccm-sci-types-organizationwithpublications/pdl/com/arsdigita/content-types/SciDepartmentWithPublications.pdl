model com.arsdigita.cms.contenttypes;

import com.arsdigita.kernel.ACSObject;
import com.arsdigita.cms.*;

object type SciDepartmentWithPublications extends SciDepartment {
    reference key ( ct_sciorga_departments_with_publications.department_id );
}

association {
    SciDepartment[0..n] departments = join ct_publications.publication_id
                                        to ct_department_publication_map.publication_id,
                                       join ct_department_publication_map.department_id
                                         to ct_sciorga_departments_with_publications.department_id;

    Publication[0..n] publications = join ct_sciorga_departments_with_publications.department_id
                                       to ct_department_publication_map.department_id,
                                     join ct_department_publication_map.publication_id
                                       to ct_publications.publication_id;     

    Integer[0..1] publicationOrder = ct_department_publication_map.publication_order INTEGER;
}

query getIdsOfPublicationsOfSciDepartment {
    BigDecimal publicationId;
    String objectType;

    do {
        select ct_department_publication_map.publication_id, acs_objects.object_type
        from ct_department_publication_map join acs_objects on ct_department_publication_map.publication_id = acs_objects.object_id
        where ct_department_publication_map.department_id = :department
    } map {
        publicationId = ct_department_publication_map.publication_id;
        objectType = acs_objects.object_type;
    }
}

query getIdsOfWorkingPapersOfSciDepartment {
    BigDecimal workingPaperId;
    String objectType;

    do {
        select ct_department_publication_map.publication_id, acs_objects.object_type
        from ct_department_publication_map join acs_objects on ct_department_publication_map.publication_id = acs_objects.object_id
        where ct_department_publication_map.department_id = :department and acs_objects.object_type = 'com.arsdigita.cms.contenttypes.WorkingPaper'
    } map {
        workingPaperId = ct_department_publication_map.publication_id;
        objectType = acs_objects.object_type;
    }
}