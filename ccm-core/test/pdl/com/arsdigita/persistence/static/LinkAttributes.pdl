//
// Copyright (C) 2001-2004 Red Hat Inc. All Rights Reserved.
//
// This library is free software; you can redistribute it and/or
// modify it under the terms of the GNU Lesser General Public License
// as published by the Free Software Foundation; either version 2.1 of
// the License, or (at your option) any later version.
//
// This library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
// Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public
// License along with this library; if not, write to the Free Software
// Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
//
// $Id: LinkAttributes.pdl 287 2005-02-22 00:29:02Z sskracic $
// $DateTime: 2004/08/16 18:10:38 $
model examples;

object type Article {
    BigInteger[1..1] id = t_articles.article_id INTEGER;
    String[0..1] text   = t_articles.text CLOB;

    object key (id);
}

object type Image {
    BigInteger[1..1] id = t_images.image_id INTEGER;
    Blob[0..1] bytes    = t_images.bytes BLOB;

    object key (id);
}

association {
    Article[0..n] articles = join t_images.image_id
                               to t_article_image_map.image_id,
                             join t_article_image_map.article_id
                               to t_articles.article_id;
    Image[0..n] images = join t_articles.article_id
                           to t_article_image_map.article_id,
                         join t_article_image_map.image_id
                           to t_images.image_id;

    String[0..1] caption = t_article_image_map.caption VARCHAR(4000);
    User[0..1] user = join t_article_image_map.user_id to t_users.user_id;

    retrieve images {
        do {
            select m.caption, m.user_id, i.*
            from t_article_image_map m, t_images i
            where article_id = :articles.id
            and m.image_id = i.image_id
        } map {
            images.id = i.image_id;
            images.bytes = i.bytes;
            caption = m.caption;
            user.id = m.user_id;
        }
    }

    retrieve articles {
        do {
            select m.caption, m.user_id, a.*
            from t_article_image_map m, t_articles a
            where image_id = :images.id
            and m.article_id = a.article_id
        } map {
            user.id = m.user_id;
            articles.id = a.article_id;
            articles.text = a.text;
            caption = m.caption;
        }
    }

    add images {
        do {
            insert into t_article_image_map
            (image_id, article_id, caption, user_id)
            values
            (:images.id, :articles.id, :caption, :user.id)
        }
    }

    add articles {
        do {
            insert into t_article_image_map
            (image_id, article_id, caption, user_id)
            values
            (:images.id, :articles.id, :caption, :user.id)
        }
    }


    // this must be here because of the way the association code works
    add user {}

    update {
        do {
            update t_article_image_map
            set caption = :caption
            where article_id = :articles.id
            and image_id = :images.id
        }
    }

    remove images {
        do {
            delete from t_article_image_map
            where image_id = :images.id
            and article_id = :articles.id
        }
    }

    remove articles {
        do {
            delete from t_article_image_map
            where image_id = :images.id
            and article_id = :articles.id
        }
    }
}
